apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'


import org.ajoberstar.grgit.Grgit

ext {
    // Read the app version information
    def git = Grgit.open(currentDir: projectDir)
    def gitVersionName = git.describe().replaceFirst("v", "") // Optional v in the beginning
    def s = gitVersionName.split("-")
    def sp = s[0].split("\\.")
    versionMajor = Integer.parseInt(sp[0])   // 0..n
    versionMinor = Integer.parseInt(sp[1])   // 0..9
    versionFix = Integer.parseInt(sp[2])   // 0..99
    versionBuild = Integer.parseInt(s.size() < 2 ? "0" : s[1])
}

def calculateVersionName = {
    // versionMajor = 1, versionMinor = 2
    // - versionFix = 0 && versionBuild = 3     -> 1.2-3
    // - versionFix = 3 && versionBuild = 4     -> 1.2.3-4
    // - versionFix = 0 && versionBuild = 0    -> 1.2
    return "${versionMajor}.${versionMinor}.${versionFix}" + ((versionBuild > 0) ? "-${versionBuild}" : "")
}


android {
    compileSdkVersion 33
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 33
        versionName calculateVersionName()
        versionCode 1

        // buildConfigField 'String', 'VERSION_NAME', "\"${versionName}\""
        // buildConfigField 'int', 'VERSION_CODE', "1"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}


dependencies {
    implementation 'com.ninchat:client:1.4.1'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'org.greenrobot:eventbus:3.2.0'

    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxkotlin:3.0.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2'


    testImplementation 'org.mockito:mockito-android:3.3.3'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'org.robolectric:robolectric:4.3'

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testImplementation group: 'org.json', name: 'json', version: '20200518'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.2"

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'com.airbnb.android:paris:1.7.3'
    implementation 'com.ninchat:libwebrtc:106.0.3'
}

apply plugin: 'maven'
apply plugin: 'maven-publish'

publishing {
    repositories {
        maven {
            def deployPath = file("${project.rootDir}")
            url "file://${deployPath.absolutePath}"
        }
    }
    publications {
        maven(MavenPublication) {
            groupId "com.ninchat"
            artifactId "libwebrtc"
            version "106.0.3"

            artifact("../webrtc-sdk/libwebrtc-106.0.3.aar")
        }
    }

}

uploadArchives {
    repositories.mavenDeployer {
        def deployPath = file("${project.rootDir}")
        repository(url: "file://${deployPath.absolutePath}")

        pom.project {
            groupId "com.ninchat"
            artifactId "sdk"
            version calculateVersionName()
        }
    }
}
