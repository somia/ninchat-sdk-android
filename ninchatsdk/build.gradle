apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'


import org.ajoberstar.grgit.Grgit

ext {
    // Read the app version information
    def git = Grgit.open(currentDir: projectDir)
    def gitVersionName = git.describe().replaceFirst("v", "") // Optional v in the beginning
    def s = gitVersionName.split("-")
    def sp = s[0].split("\\.")
    versionMajor = Integer.parseInt(sp[0])   // 0..n
    versionMinor = Integer.parseInt(sp[1])   // 0..9
    versionFix = Integer.parseInt(sp[2])   // 0..99
    versionBuild = Integer.parseInt(s.size() < 2 ? "0" : s[1])
}

def calculateVersionName = {
    // versionMajor = 1, versionMinor = 2
    // - versionFix = 0 && versionBuild = 3     -> 1.2-3
    // - versionFix = 3 && versionBuild = 4     -> 1.2.3-4
    // - versionFix = 0 && versionBuild = 0    -> 1.2
    return "${versionMajor}.${versionMinor}.${versionFix}" + ((versionBuild > 0) ? "-${versionBuild}" : "")
}


android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 28
        versionName calculateVersionName()

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation 'com.ninchat:client:1.4.0-1'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'org.webrtc:google-webrtc:1.0.26885'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation 'org.greenrobot:eventbus:3.2.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-android:3.3.3'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'org.robolectric:robolectric:4.3'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation group: 'org.json', name: 'json', version: '20200518'

}

apply plugin: 'maven'

uploadArchives {
    repositories.mavenDeployer {
        def deployPath = file("${project.rootDir}")
        repository(url: "file://${deployPath.absolutePath}")
        pom.project {
            groupId "com.ninchat"
            artifactId "sdk"
            version calculateVersionName()
        }
    }
}
